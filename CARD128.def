(* (C) 2009, 2010 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

DEFINITION MODULE CARD128 [ZTYPE];

(* 128-bit Unsigned Integer Numbers *) 


IMPORT ScalarDigitCount;

FROM FileIO IMPORT File; (* required for IO *)


(* CARD128 type *)

TYPE
    CARD128 = OPAQUE RECORD
        value : ARRAY 16 OF OCTET; (* 128 bits *)
    END; (* CARD128 *)


(* Maximum digits in native radix *)

CONST
    maxDigitCount = { 32, 0 } :: ScalarDigitCount;
    (* maximum of 32 base-16 digits significand, no exponent *)


(* Z-Type bindings to operators, pervasives and primitives: *)


(* Range *) 

CONST [TMIN] minValue = 0;
(* Smallest value of type CARD128.
   This value is bound to TMIN for type CARD128. *) 

CONST [TMAX] maxValue =
    { 0FFH, 0FFH, 0FFH, 0FFH, 0FFH, 0FFH, 0FFH, 0FFH,
      0FFH, 0FFH, 0FFH, 0FFH, 0FFH, 0FFH, 0FFH, 0FFH };
(* Largest value of type CARD128.
   This value is bound to TMAX for type CARD128. *)


(* Literal assignment *)

PROCEDURE [:=] assign ( VAR assignTo : CARD128; literal : ARRAY OF CHAR );
(* Converts string literal to a CARD128 value and assigns it to assignTo.
   This procedure is bound to the := operator for literal assignment. *)


(* Type conversions *)

PROCEDURE [::] toCARD  ( n : CARD128 ) : CARDINAL;
(* Converts CARD128 value n to a CARDINAL value and returns it. This
   function is bound to the :: operator for CARD128 to CARDINAL conversion.*)

PROCEDURE [::] toINT ( n : CARD128 ) : INTEGER; 
(* Converts CARD128 value n to an INTEGER value and returns it. This
   function is bound to the :: operator for CARD128 to INTEGER conversion. *)

PROCEDURE [::] fromCARD  ( n : CARDINAL ) : CARD128;
(* Converts CARDINAL value n to a CARD128 value and returns it. This function
   is bound to the :: operator for CARDINAL to CARD128 conversion. *)

PROCEDURE [::] fromINT  ( n : INTEGER ) : CARD128;
(* Converts INTEGER value n to a CARD128 value and returns it. This function
   is bound to the :: operator for INTEGER to CARD128 conversion. *)


(* Monadic arithmetic operations *) 

PROCEDURE [ABS] abs ( n : CARD128 ) : CARD128;
(* Returns the absolute value of CARD128 value n.
   This function is bound to pervasive function ABS for type CARD128. *)

PROCEDURE [ODD] odd ( n : CARD128 ) : BOOLEAN;
(* Returns TRUE if CARD128 value n is odd, otherwise FALSE.
   This function is bound to pervasive function ODD for type CARD128. *)


(* Dyadic arithmetic operations *) 

PROCEDURE [+] add ( n1, n2 : CARD128 ) : CARD128;
(* Adds CARD128 values n1 and n2 and returns the result.
   This function is bound to the + operator for type CARD128. *)

PROCEDURE [-] sub ( n1, n2 : CARD128 ) : CARD128;
(* Subtracts CARD128 value n2 from n1 and returns the result.
   This function is bound to the - operator for type CARD128. *)

PROCEDURE [*] multiply ( n1, n2 : CARD128 ) : CARD128;
(* Multiplies CARD128 values n1 and n2 and returns the result.
   This function it bound to the * operator for type CARD128. *)

PROCEDURE [/] divide ( n1, n2 : CARD128 ) : CARD128;
(* Divives CARD128 value n1 by n2 and returns the result.
   This function is bound to the / operator for type CARD128. *)


(* Relational operations *) 

PROCEDURE [=] isEqual ( n1, n2 : CARD128 ) : BOOLEAN;
(* Returns TRUE if CARD128 values n1 and n2 are equal, otherwise FALSE.
   This function is bound to operators = and # for tyep CARD128. *)

PROCEDURE [<] isLess ( n1, n2 : CARD128 ) : BOOLEAN;
(* Returns TRUE if CARD128 value n1 is less than n2, otherwise FASLE.
   This function is bound to operators < and >= for type CARD128. *)

PROCEDURE [>] isGreater ( n1, n2 : CARD128 ) : BOOLEAN;
(* Returns TRUE if CARD128 value n1 is greater than n2, otherwise FALSE.
   This function is bound to operators > and <= for type CARD128. *)


(* Scalar conversion primitives *)

PROCEDURE [TO] toSXF ( n : CARD128; VAR s : ARRAY OF CHAR );
(* Converts CARD128 value n to a string in scalar exchange format.
   This procedure is used to synthesise conversions to other scalar types
   when no direct conversion path exists. *)

PROCEDURE [FROM] fromSXF ( VAR n : CARD128; CONST s : ARRAY OF CHAR );
(* Converts a string in scalar exchange format to a CARD128 value.
   This procedure is used to synthesise conversions from other scalar types
   when no direct conversion path exists. *)


(* IO operations *)

PROCEDURE Read( infile : File; VAR n : CARD128 );
(* Reads the textual representation of a CARD128 value from stream infile
   - any leading whitespace is skipped
   - any remaining characters that are part of the numeral being read are
     removed from infile
   - the numeric value of the numeral string read is assigned to the variable
     passed in for n
   - the file status is set to any of:
     success, outOfRange, wrongFormat, endOfLine, or endOfInput. This
   procedure is substituted for invocations of READ with a CARD128 argument. *)

PROCEDURE Write( outfile : File; CONST n : CARD128 );
(* Writes the textual representation of value n to stream outfile. This
   procedure is substituted for invocations of WRITE with a CARD128 argument.*)

PROCEDURE WriteF ( outfile      : File;
                   CONST fmtStr : ARRAY OF CHAR;
                   items        : VARIADIC OF CONST CARD128 );
(* Writes a formatted textual representation of one or more CARD128 values to
   output stream outfile. The output format is determined by fmtStr. This
   procedure is substituted for invocations of WRITEF with one or more
   CARD128 arguments. *)

END CARD128. (* one-twenty-eight *)