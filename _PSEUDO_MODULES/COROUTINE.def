(*!m2r10*) (* (C) 2015 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

DEFINITION MODULE COROUTINE;


CONST DefaultStackSize = 4096;

CONST SuppportsAutomaticStackResize = <implementation defined boolean value>;


(* A coroutine type is declared using the COROUTINE type constructor syntax:

TYPE Coro = COROUTINE ( AssocProcTypeIdent ); *)


(* Procedures to be associated with a coroutine must be explicitly marked:

PROCEDURE [COROUTINE] assocProc ( p1 : T1; p2 : T2; p3 : T3; ... ) : Result; *)


(* Coroutines have one of three states *)

TYPE State = ( waiting, running, done );


(* General Control Facilities *)

(* Creating a Coroutine *)

PROCEDURE [NEW] new
  ( NEW co : <CoroutineType>;
    p : AssocProcType; stackSize : CARDINAL; p1 : T1; p2 : T2; p3 : T3 ... );
(* Allocates and initialises a new coroutine, puts co into state waiting.
   When zero is passed for stackSize, DefaultStackSize is used.
   The procedure is bound to the NEW statement.
   
   Usage:
   
   NEW co := { assocProc, 0, p1, p2, p3, ... }; *)


(* Activating a Coroutine *)

PROCEDURE AWAKE ( co : <CoroutineType> ) : ResultType;
(* Passes control to coroutine co, puts co into state running.
   Returns result if co yields or returns a result.
   Raises runtime fault if co is in state done unless intercepted by a handler.
   
   Usage:
   
   COROUTINE.AWAKE(co);
   result := COROUTINE.AWAKE(co); *)


(* Suspending a Coroutine *)

(* The YIELD statement suspends a coroutine.
   
   Suspending a coroutine yields control and returns a result to the caller,
   thereby putting the coroutine into state waiting.
   
   The result must match the return type of the associated procedure.
   No result must be returned if the associated procedure is not a function.
   
   Usage:
   
   YIELD;
   YIELD result; *)


(* Terminating a Coroutine *)

(* The RETURN statement terminates a coroutine.
   
   Terminating a coroutine returns control and a final result to the caller,
   thereby putting the coroutine into state done.
   
   The result must match the return type of the associated procedure.
   No result must be returned if the associated procedure is not a function.

   Usage:
   
   RETURN;
   RETURN result; *)


(* Checking the Termination State of a Coroutine *)

PROCEDURE DONE ( list : ARGLIST OF <CoroutineType> ) : BOOLEAN;
(* Returns TRUE if all coroutines in the argument list have terminated,
   returns FALSE if any coroutine in the list has not yet terminated.
   
   Usage:
   
   REPEAT
     COROUTINE.AWAKE(co1);
     COROUTINE.AWAKE(co2);
     COROUTINE.AWAKE(co3)
   UNTIL COROUTINE.DONE(co1, co2, co3); *)


(* Deallocating a Coroutine *)

PROCEDURE [RELEASE] release (VAR co : <CoroutineType> );
(* Deallocates coroutine co and passes NIL back in co.
   Raises a runtime fault if CurrentState(co) is not done.
   This procedure is bound to the RELEASE statement.
   
   Usage:
   
   RELEASE co; *)


(* Fine-Grained Control Facilities *)

(* Obtaining the State of a Coroutine *)

PROCEDURE currentState ( co : <CoroutineType> ) : State;
(* Returns the current state of coroutine co. *)


(* Scheduling a Coroutine for Premature Termination *)

PROCEDURE scheduleTermination ( co : <CoroutineType> );
(* Puts co into state done immediately if it is waiting, or after its next
   yield if it is running. Invocation is ignored if co is already done. *)


PROCEDURE terminationScheduled ( co : <CoroutineType> ) : BOOLEAN;
(* Returns TRUE if co has been scheduled for termination, otherwise FALSE. *)


(* Intercepting Attempts to Activate Dead Coroutines *)

PROCEDURE InstallAwakeDeadCoroHandler
  ( co : <CoroutineType>; handler : <HandlerType> );
(* Installs a user defined fault event handler for coroutine co. By defuault,
   an attempt to call AWAKE on a coroutine that has already terminated will
   raise a runtime fault. If a handler is installed, no runtime fault will be
   raised and the handler's return value is returned by AWAKE. The signature
   of the handler must match the signature of the procedure associated with
   the coroutine. A handler may be removed by passing NIL for handler.
   
   Usage:
   
   Signature of Coroutine Associated Procedure
   
   PROCEDURE [COROUTINE] Foo ( n : CARDINAL ) : INTEGER;
   
   Matching User Handler
   
   PROCEDURE IgnoreAwakeDeadCoro ( n : CARDINAL ) : INTEGER;
   BEGIN
     RETURN -1;
   END IgnoreAwakeDeadCoro;
   
   Installing the Handler
   
   InstallAwakeDeadCoroHandler(co, IgnoreAwakeDeadCoro); *) 


END COROUTINE.