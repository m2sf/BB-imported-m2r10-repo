(* (C) 2009-2013 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

DEFINITION MODULE BS16 [ProtoStaticSet]; (* sixteen one-six *)

(* 16-bit statically allocatable bitsets *)


(* Imports *)

FROM FileIO IMPORT File; (* required for IO *)


(* Constants *)

CONST MaxCapacity = 16;

CONST RequiredOctets = 2;

CONST [DESCENDING] supportsDescendingFor = TRUE;
(* Enables the use of FOR DESCENDING syntax. *)


(* BS16 Type *)

TYPE BS16 = OPAQUE RECORD
    elements : ARRAY ReqOctets OF OCTET;
END; (* BS16 *)


PROCEDURE [TLIMIT] capacityLimit : LONGCARD;
(* Returns the value of constant MaxCapacity. This function is bound to
   pervasive function TLIMIT for type operand BS16. *)


PROCEDURE [TSIZE] storageSize : LONGCARD;
(* Returns the value of constant RequiredOctets. This function is bound
   to pervasive function TSIZE for type operand BS16. *)


(* Conversion *)

(* none *)


(* Storage, Removal, Retrieval and Inspection of Contents *)

PROCEDURE [STORE] storeElements
  ( VAR bitset : BS16; valueList : VARIADIC OF CARDINAL );
(* Stores the elements given by valueList in BS16 variable bitset.
   This procedure is bound to pervasive procedure STORE for first
   operands of type BS16. *)


PROCEDURE [REMOVE] removeElements
  ( VAR bitset : BS16; valueList : VARIADIC OF CARDINAL );
(* Removes the elements given by valueList from BS16 variable bitset. Re-
   moves all elements if no values are passed in valueList. This procedure
   is bound to pervasive procedure REMOVE for first operands of type BS16. *)


PROCEDURE [RETRIEVE] membershipForElement
  ( CONST bitset : BS16; value : CARDINAL ) : BOOLEAN;
(* Calls function isElement and returns its result. This function is bound
   to pervasive function RETRIEVE for first operands of type BS16. *)


PROCEDURE [IN] isElement ( CONST bitset : BS16; value : CARDINAL ) : BOOLEAN;
(* Returns TRUE if value is an element of bitset, returns FALSE otherwise.
   This function is bound to operator IN for left operands of type CARDINAL
   and right operands of type BS16. *)


PROCEDURE [COUNT] elementCount ( CONST bitset : BS16 ) : LONGCARD;
(* Returns the number of elements in BS16 set. This function is bound
   to pervasive function COUNT for type BS16. *)


(* Iteration *)

TYPE ForLoopBodyProc = PROCEDURE ( (* value : *) CARDINAL );

PROCEDURE [FOR] forIterator
  ( CONST bitset : BS16;
    forLoopBody : ForLoopBodyProc; ascending : BOOLEAN );
(* Iterates over all elements in bitset, executing procedure forLoopBody
   for each element stored in bitset. If TRUE is passed in for ascending,
   the iteration order is ascending, otherwise descending. This procedure
   is bound to built-in syntax FOR IN for type BS16. *)


(* Set Operations *) 

PROCEDURE [+] union ( CONST bitset1, bitset2 : BS16 ) : BS16;
(* Returns the union of BS16 values bitset1 and bitset2. This function
   is bound to the + operator for operands of type BS16. *)

PROCEDURE [-] diff ( CONST bitset1, bitset2 : BS16 ) : BS16;
(* Returns the set difference of BS16 values bitset1 and bitset2. This
   function is bound to the - operator for operands of type BS16. *)

PROCEDURE [*] intersection ( CONST bitset1, bitset2 : BS16 ) : BS16;
(* Returns the intersection of BS16 values bitset1 and bitset2.  This
   function it bound to the * operator for operands of type BS16. *)

PROCEDURE [/] symdiff ( CONST bitset1, bitset2 : BS16 ) : BS16;
(* Returns the symmetric set difference of BS16 values bitset1 and bitset2.
   This function is bound to the / operator for operands of type BS16. *)


(* Relational operations *)

PROCEDURE [=] isEqual ( CONST bitset1, bitset2 : BS16 ) : BOOLEAN;
(* Returns TRUE if BS16 values bitset1 and bitset2 are equal, otherwise
   FALSE. This function is used to synthesise bindings to the operators
   = and # for operands of type BS16. *)


PROCEDURE [SUBSET] isSubset ( CONST bitset1, bitset2 : BS16 ) : BOOLEAN;
(* Returns TRUE if BS16 bitset1 is a subset of bitset2, otherwise FALSE.
   This function is used to synthesise bindings to the operators >, >=,
   < and <= for operands of type BS16. *)


(* IO operations *)

(* The EBNF for the textual representation of BS16 values is:
    bitsetValue : simpleFormat | compactFormat | bitGroupFormat ;
    simpleFormat : bit+ ;
    compactFormat : base16Digit+ ;
    bitGroupFormat : "{" bit+ ( groupSeparator? bit+ )* "}" ;
    groupSeparator : " " | "." ;
    bit  : "0" | "1" ;
    base16Digit : "0" .. "9" | "A" .. "F" ;
   The number of bits shown is equal to the bitwidth of the BS16 type. *)

PROCEDURE Read ( infile : File; VAR bitset : BS16 );
(* Reads the textual representation of a BS16 value from stream infile
   - any leading whitespace is skipped
   - any remaining characters that are part of the value being read are
     removed from infile
   - the textual representation of the value read is assigned to set
   - the file status is set to any of:
     success, outOfRange, wrongFormat, endOfLine, or endOfInput. This
   procedure is substituted for invocations of READ with a BS16 argument. *)

PROCEDURE Write ( outfile : File; CONST bitset : BS16 );
(* Writes the value of BS16 bitset in simple format to stream outfile. This
   procedure is substituted for invocations of WRITE with a BS16 argument.*)

PROCEDURE WriteF ( outfile      : File;
                   CONST fmtStr : ARRAY OF CHAR;
                   items        : VARIADIC OF CONST BS16 );
(* Writes a formatted textual representation of one or more BS16 values to
   output stream outfile. The output format is determined by fmtStr. This
   procedure is substituted for invocations of WRITEF with one or more
   BS16 arguments. *)


(* Introspection *)

PROCEDURE capacity ( CONST bitset : BS16 ) : LONGCARD;
(* Always returns the value of TLIMIT(BS16). *)


PROCEDURE entryLimit ( CONST bitset : BS16 ) : LONGCARD;
(* Always returns the value of TLIMIT(BS16). *)


PROCEDURE isOrdered ( CONST bitset : BS16 ) : BOOLEAN;
(* Always returns TRUE because type BS16 is ordered. *)


PROCEDURE isResizable ( CONST bitset : BS16 ) : BOOLEAN;
(* Always returns FALSE because type BS16 is static *)


END BS16.