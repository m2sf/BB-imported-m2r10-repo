(* (C) 2009-2013 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

DEFINITION MODULE FileIO;

(* Driver for File Based IO *)

(* This module interprets the contents of files as a stream of octets without
   any particular structure. The module represents the lowest level interface
   for file based IO in the standard library. *)

IMPORT Integers;
ALIAS FOR CADRDINAL64 OF Integers; (* assuming 64-bit file systems *)


(* File accessor, file status and file mode *)

TYPE
    File = OPAQUE; (* implementation defined file accessor *)
    
    (* Any attempt to pass an invalid file accessor to a file operation other
       than opening a file shall raise runtime fault InvalidAccessor. *)
        
    Status = ( success,               (* operation completed successfully   *)
               fileNotFound,          (* no file found with this filename   *)
               nameTooLong,           (* the passed in filename is too long *)
               illegalCharsInName,    (* illegal chars in passed filename   *)
               invalidMode,           (* the passed in mode is invalid      *)
               alreadyOpen,           (* the passed in file is already open *)
               mayNotOpenDirectory,   (* attempt to open a directory        *)
               accessDenied,          (* the filesystem denied file access  *)
               accessBeyondEOF,       (* attempt to read past end of file   *)
               fileSizeLimitExceeded, (* attempt to write past size limit   *)
               openFileLimitExceeded, (* attempt to open too many files     *)
               operationNotSupported, (* unsupported operation attempted    *)
               deviceFull,            (* the device capacity is exceeded    *)
               deviceError );         (* the device reported a failure      *)

(* The mode of a file consists of four flags: read, write, append and sync.
   These flags may be combined arbitrarily, subject to the following rules:
   
     1) a file mode may not be the empty set
     2) append may not be set if read is set
     3) append may only be set if write is set
     4) sync may only be set if write is set
     
   Any attempt to open or reopen a file, passing an invalid file mode shall
   fail with status invalidMode. *)

TYPE ModeFlags = ( read,              (* reading is permitted if set        *)
                   write,             (* writing is permitted if set        *)
                   append,            (* writing always appends if set      *)
                   sync );            (* writing always flushes if set      *)

TYPE Mode = SET OF ModeFlags;


(* Alias types for implementation dependent file position and file size *)

    FileSize = ALIAS OF CARDINAL64; (* must always be an unsigned type *)
    
    FilePos = ALIAS OF FileSize;


(* Predefined file modes *)

CONST
    write = { Mode.write };
    readOnly = { Mode.read };
    readWrite = { Mode.read, Mode.write };
    append = { Mode.write, Mode.append };
    
    defaultBufferSize = <implementation defined value>;


(* Parameter consistency checks *)

PROCEDURE IsValidAccessor ( file : File ) : BOOLEAN;
(* Returns TRUE if <file> is a valid file accessor, otherwise FALSE. *)

PROCEDURE IsValidMode ( mode : Mode ) : BOOLEAN;
(* Returns TRUE if <mode> is a valid file mode, otherwise FALSE. *)

PROCEDURE IsValidPos ( file : File; pos : FilePos ) : BOOLEAN;
(* Returns TRUE if <pos> is a valid file position for <file>, otherwise FALSE. *)


(* Opening files *)

PROCEDURE Open ( VAR file       : File;
                 CONST filename : ARRAY OF CHAR;
                 mode           : Mode;
                 VAR status     : Status );
(* Opens the file <filename> in file mode <mode>.  If sucessful passes a file
   accessor back in <file>,  otherwise passes NIL back in <file>.  Sets the
   file position depending on <mode>.  The status of the operation is passed
   back in <status>. *)

PROCEDURE OpenWithBuffer ( VAR file         : File;
                           CONST filename   : ARRAY OF CHAR;
                           mode             : Mode;
                           VAR buffer       : ARRAY OF OCTET;
                           VAR status       : Status );
(* Opens the file <filename> in file mode <mode> using <buffer> as a custom
   file buffer.  If successful passes a file accessor back in <file>,  other-
   wise passes NIL back in <file>.  Sets the file position depending on <mode>.
   The status is passed back in <status>. *)

PROCEDURE ReOpen ( file : File; mode : Mode );
(* Flushes the file associated with file accessor <file> and changes its mode
   to <mode>. Sets the file position depending on <mode>. *)

(* Opening or reopening a file in a mode whose append flag is not set causes
   the file position to be set to the beginning of the file.  Opening or re-
   opening a file in a mode whose append flag is set causes the file position
   to be set to the end of the file.  An attempt to open a file that is a di-
   rectory fails with status code mayNotOpenDirectory. *)


(* File mode, status and name *)

PROCEDURE ModeOf ( file : File ) : Mode;
(* Returns the file mode of file accessor <file>. *)

PROCEDURE StatusOf ( file : File ) : Status;
(* Returns the status of the most recent operation for <file>. *)

PROCEDURE NameLen ( file : File ) : CARDINAL;
(* Returns the length of the filename associated with <file>. *)

PROCEDURE GetName ( file : File; VAR filename : ARRAY OF CHAR );
(* Passes the name  of the file associated  with file accessor <file> back in
   <filename>.  If the name exceeds the capacity of <filename>  the operation
   fails with status nameTooLong, passing an empty string back in <filename>. *)


(* File position operations *)

PROCEDURE EOF ( file : File ) : BOOLEAN;
(* Returns TRUE if the end of the file associated with file accessor <file>
   has been reached, otherwise FALSE. *)

PROCEDURE CurrentPos ( file : File ) : FilePos;
(* Returns the current read/write position for file accessor <file> where a
   value of TMIN(FilePos) represents the beginning of a file. *)

PROCEDURE LastValidSetPos ( file : File ) : FilePos;
(* Returns the most advanced position that may be passed to SetPos for file
   accessor <file>.  The return value depends on length and mode of <file>
   
   (1) if <file> is in append mode, the function returns zero and sets the
       file's status to OperationNotSupported.
   (2) if <file> is empty, then TMIN(FilePos) is returned.
   (3) if <file> is not empty and has its write flag set, then the position
       of the last octet in <file> plus one is returned.
   (4) if <file> is not empty and has its read but not its write flag set,
       then the position of the last octet in <file> is returned. *) 

PROCEDURE SetPos ( file : File; pos : FilePos );
(* Sets the  read/write position  for  file accessor <file>  to <pos>.  For
   any empty file, <pos> must be zero. For any non-empty file in read mode,
   <pos> must be  less than  LastValidWritePos.  For any  non-empty file in
   write mode, <pos> must be  less than or equal  to LastValidWritePos. *)

PROCEDURE Advance ( file : File; offset : FilePos );
(* Advances the read/write position for file accessor <file> by <offset>. *)

PROCEDURE Rewind ( file : File );
(* Sets the read/write position for file accessor <file>  to the  beginning
   of the file and resets its end-of-file status. *)

(* Any attempt to call  LastValidWritePos, SetPos, Advance or Rewind  on  a
   file accessor  whose file mode  has the append flag set  shall fail with
   status operationNotSupported.  Any attempt to set a file's read position
   past the end of the file  shall fail  with status  accessBeyondEOF.  Any
   attempt to set a file's write position past LastValidWritePos shall fail
   with status accessBeyondEOF. *)


(* Read and write operations *)

PROCEDURE Read ( file : File; VAR data : OCTET );
(* Reads one octet of data at the current position of <file>, passes it back
   in <data> and advances the read/write position of <file> by one. *)

PROCEDURE Lookahead ( file : File; VAR data : OCTET );
(* Reads the first lookahead octet of <file>, passes it back in <data>, but
   does NOT advance the read/write position of <file>.  If the lookahead
   octet lies beyond the end of the file then no data is passed back and
   the file status is set to accessBeyondEOF. *)

PROCEDURE LA2 ( file : File; VAR data : OCTET );
(* Reads the second lookahead octet of <file>, passes it back in <data>, but
   does NOT advance the read/write position of <file>.  If the second look-
   ahead octet lies beyond the end of the file then no data is passed back
   and the file status is set to accessBeyondEOF. *)

PROCEDURE ReadBlock ( file : File;
                      VAR data : ARRAY OF OCTET; VAR octetsRead : FileSize );
(* Reads a block of data at the current position of <file>. Passes the block
   of data back in <data> and the number of octets read in <octetsRead>.
   The read/write position of <file> is advanced accordingly. *)

(* Any attempt to read or lookahead read from a file whose read flag is not
   set shall fail with status operationNotSupported. *)


PROCEDURE Write ( file : File; data : OCTET );
(* Performs a write or append operation depending on the mode of <file>.
   In write mode, the procedure writes one octet in <data> to the current
   position of <file>. In append mode, the procedure atomically sets the
   current read/write position of <file> to the end of the file and appends
   one octet in <data> to the end of the file. In either mode, the current
   read/write position of <file> is advanced by one after the data has been
   written. *)

PROCEDURE WriteBlock ( file : File;
                       data : ARRAY OF OCTET; VAR octetsWritten : FileSize );
(* Performs a write or append operation depending on the mode of <file>.
   In write mode, the procedure writes the block of data passed in <data>
   starting at the current read/write position of <file>. In append mode, the
   procedure atomically sets the current read/write position of <file> to the
   end of the file and appends the block of data passed in <data> to the end
   of the file. In either mode, the current read/write position of <file> is
   advanced by the number of octets written and the number of octets written
   is passed back in <octetsWritten> after the data has been written. *)

PROCEDURE Flush ( file : File );
(* Writes unwritten data in any buffer of <file> to its associated file. *)

(* Any attempt to write to or flush a file whose write flag is not set shall
   fail with status operationNotSupported. *)


(* Closing files *)

PROCEDURE Close ( VAR file : File; VAR status : Status );
(* Performs Flush on <file>, closes the associated file and passes NIL back
   in <file>.  The status of the operation is passed back  in <status>. *)

PROCEDURE CloseAll;
(* Closes all open files. *)


(* Implementation defined accessors to special files *)

(* Special files  are automatically opened when module FileIO is initialised.
   The file mode of a special file is either read  or write.   EOF always re-
   turns FALSE and CurrentPos always returns TMIN(FilePos) for special files.
   Any attempt to open a special file will fail with status alreadyOpen.  Any
   attempt to call ReOpen, NameLen, GetName, LastValidSetPos, SetPos, Rewind,
   Advance, Lookahead, LA2, ReadBlock, Flush or Close  will fail with  status
   operationNotSupported.  NameLen  and LastValidSetPos will return zero  and
   GetName will pass an empty string. *)

PROCEDURE IsSpecialFile ( file : File ) : BOOLEAN;
(* Returns TRUE if file is associated with a special file, otherwise FALSE. *)
   
PROCEDURE DefaultInFile : File;
(* Returns the file accessor associated with an implementation defined source
   for program input, open in read mode, usually a terminal. *)

PROCEDURE DefaultOutFile : File;
(* Returns the file accessor associated with an implementation defined desti-
   nation for program output, open in write mode, usually a terminal. *)

PROCEDURE DefaultErrFile : File;
(* Returns the file accessor associated with an implementation defined desti-
   nation for program error output, open in write mode, usually a terminal. *)

PROCEDURE DefaultNullFile : File;
(* Returns the file accessor associated with the null device, open in write
   mode.  Write operations on this file have no effect. *)


END FileIO.