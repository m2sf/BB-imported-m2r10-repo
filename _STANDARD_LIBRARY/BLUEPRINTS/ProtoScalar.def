(* (C) 2012-2015 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

BLUEPRINT ProtoScalar [ProtoNumeric];

(* General Blueprint for Scalar ADTs *)


(* Required Module Type *)

MODULE TYPE = RECORD;
(* All scalar ADTs shall be statically allocatable. *)

LITERAL = INTEGER | REAL;
(* Integer or real number literals may be used. *)


(* Required Constants *)

(* Classification *)

CONST [TCOMP] isComputational = TRUE;
(* Scalar ADTs are always computational. *)

CONST [TNUM] isNumeric = TRUE;
(* Scalar ADTs are always numeric. *)

CONST isScalar = TRUE;
(* Sclalar ADTs are always scalar. *)

CONST isOrdinal : BOOLEAN;
(* Scalar ADTs shall define whether they are ordinal or not. *)


(* Scalar Properties *)

CONST [TSIGNED] isSigned : BOOLEAN;
(* Scalar ADTs shall define whether they are signed or unsigned. *)

CONST [TBASE] base : CARDINAL; (* either 2 or 10. *)
(* Scalar ADTs shall define the radix in which values are encoded. *) 

CONST [TPRECISION] precision : CARDINAL; (* 1 to 4000 *)
(* Scalar ADTs shall define the maximum number of encodable digits. *)

isOrdinal ->
CONST [TMINEXP] eMin = 0;
(* Ordinal Scalar ADTs shall define eMin with a value of zero. *)

NOT isOrdinal ->
CONST [TMINEXP] eMin : INTEGER;
(* Non-Ordinal Scalar ADTs shall define the smallest encodable exponent. *)

isOrdinal ->
CONST [TMAXEXP] eMax = 0;
(* Ordinal Scalar ADTs shall define eMax with a value of zero. *)

NOT isOrdinal ->
CONST [TMAXEXP] eMin : INTEGER;
(* Non-Ordinal Scalar ADTs shall define the largest encodable exponent. *)


(* Memory Management *)

CONST [TDYN] isDynamic = FALSE;
(* Scalar ADTs are always statically allocated. *)


(* Required Procedures *)

(* Introspection *)

(* Smallest Value *)

PROCEDURE [TMIN];
(* All scalar ADTs shall be required to bind to TMIN. *)


(* Largest Value *)

PROCEDURE [TMAX];
(* All scalar ADTs shall be required to bind to TMAX. *)


(* Numeric Conversion *)

(* Conversion to Scalar Exchange Format *)

PROCEDURE [SXF];
(* All scalar ADTs shall be required to bind to SXF. *)


(* Conversion from Scalar Exchange Format *)

PROCEDURE [VAL];
(* All scalar ADTs shall be required to bind to VAL. *)


(* Sign Removal *)

PROCEDURE [ABS];
(* All scalar ADTs shall be required to bind to ABS. *)


(* Sign Reversal *)

isSigned -> PROCEDURE [+/-];
(* Signed scalar ADTs shall be required to bind to Unary Minus. *)


(* Odd/Even Test *)

isOrdinal -> PROCEDURE [ODD];
(* Ordinal scalar ADTs shall be required to bind to ODD. *)


(* Arithmetic Operations *)

PROCEDURE [+];
(* All scalar ADTs shall be required to bind to + (addition). *)

PROCEDURE [-];
(* All scalar ADTs shall be required to bind to + (subtraction). *)

PROCEDURE [*];
(* All scalar ADTs shall be required to bind to * (multiplication). *)

NOT isOrdinal -> PROCEDURE [/];
(* Non-Ordinal scalar ADTs shall be required to bind to / (division). *)

isOrdinal -> PROCEDURE [DIV];
(* Ordinal scalar ADTs shall be required to bind to DIV. *)

isOrdinal -> PROCEDURE [MOD];
(* Ordinal scalar ADTs shall be required to bind to MOD. *)


(* Relational Operations *)

PROCEDURE [=];
(* All scalar ADTs shall be required to bind to = (equality). *)

(* The operation for # is synthesised from this binding. *)


PROCEDURE [<];
(* All scalar ADTs shall be required to bind to < (less-than). *)

(* The operation for >= is synthesised from this binding. *)


PROCEDURE [>];
(* All scalar ADTs shall be required to bind to > (greater-than). *)

(* The operation for <= is synthesised from this binding. *)


(* Inhibited Bindings *)

NOT isSigned -> PROCEDURE [+/-] = NONE; (* inhibited *)
(* Unsigned scalar ADTs may not be required to bind to unary minus. *)

NOT isOrdinal -> PROCEDURE [ODD] = NONE; (* inhibited *)
(* Non-Ordinal scalar ADTs may not be required to bind to DIV. *)

isOrdinal -> PROCEDURE [/] = NONE; (* inhibited *)
(* Ordinal scalar ADTs may not be required to bind to / (division). *)

NOT isOrdinal -> PROCEDURE [DIV] = NONE; (* inhibited *)
(* Non-Ordinal scalar ADTs may not be required to bind to DIV. *)

NOT isOrdinal -> PROCEDURE [MOD] = NONE; (* inhibited *)
(* Non-Ordinal scalar ADTs may not be required to bind to MOD. *)


END ProtoScalar.