(* (C) 2012-2013 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

(* Specialised Blueprint for Real Number ADTs *)

BLUEPRINT ProtoReal [ProtoScalar]; (* conforms to ProtoScalar *)

(* ProtoReal ADTs must be opaque records to be statically allocatable *)

TYPE ProtoReal = OPAQUE RECORD (* uses real number literals *) := REAL;


(* Bindings required for ProtoReal ADTs *)


(* Required Constants *)

CONST [-#] isSigned : BOOLEAN;
(* whether the ADT is signed or not *)

CONST [#^] base : CARDINAL;
(* radix of the ADT's internal representation, 2 or 10 *)

CONST [.#] precision : [1..4000] OF CARDINAL;
(* how many digits the ADT can encode in its significand *)

CONST [-^] eMin : INTEGER;
(* value of the smallest exponent the ADT can encode *)

CONST [+^] eMax : CARDINAL;
(* value of the largest exponent the ADT can encode *)


(* Bindings to pervasive functions TMIN and TMAX *)

PROCEDURE [TMIN] minValue : ProtoReal;
(* procedure to return minimal value of a real number ADT *)

PROCEDURE [TMAX] maxValue : ProtoReal;
(* procedure to return maximal value of a real number ADT *)


(* Bindings to scalar conversion primitives *)

PROCEDURE [SXF] toSXF ( value : ProtoReal; VAR sxf : ARRAY OF OCTET );
(* procedure to convert a literal to a value in scalar exchange format *)

PROCEDURE [VAL] fromSXF ( sxf : ARRAY OF OCTET; VAR value : ProtoReal );
(* procedure to convert value in scalar exchange format to the ADT *)


(* The assignment of literal values is synthesised from SXF and VAL *)


PROCEDURE [ABS] abs ( r : ProtoReal ) : ProtoReal;
(* procedure to bind to the ABS operator for operands of the ProtoReal ADT *)

PROCEDURE [NEG] neg ( r : ProtoReal ) : ProtoReal;
(* procedure to bind to the NEG operator for operands of the ProtoReal ADT. *)

(* binding NEG is only permitted if constant isSigned is defined to be TRUE. *)


(* Bindings to dyadic operations *)

PROCEDURE [+] add ( r1, r2 : ProtoReal ) : ProtoReal;
(* procedure to bind to the + operator for operands of the ProtoReal ADT *)

(* The operation for the ++ statement is synthesized as r := r + VAL(1.0) *)

PROCEDURE [-] subtract ( r1, r2 : ProtoReal ) : ProtoReal;
(* procedure to bind to the - operator for operands of the ProtoReal ADT *)

(* The operation for the -- statement is synthesized as r := r - VAL(1.0) *)

PROCEDURE [*] multiply ( r1, r2 : ProtoReal ) : ProtoReal;
(* procedure to bind to the * operator for operands of the ProtoReal ADT *)

PROCEDURE [/] divide ( r1, r2 : ProtoReal ) : ProtoReal;
(* procedure to bind to the / operator for operands of the ProtoReal ADT *)


(* Bindings to relational operations *)

PROCEDURE [=] isEqual ( r1, r2 : ProtoReal ) : BOOLEAN;
(* procedure to bind to the = operator for operands of the ProtoReal ADT *)

(* The operation for the # operator is synthesized as NOT (r1 = r2) *)

PROCEDURE [>] isGreater ( r1, r2 : ProtoReal ) : BOOLEAN;
(* procedure to bind to the > operator for operands of the ProtoReal ADT *)

(* The operation for the <= operator is synthesized as NOT (r1 > r2) *)

PROCEDURE [<] isLess ( r1, r2 : ProtoReal ) : BOOLEAN;
(* procedure to bind to the < operator for operands of the ProtoReal ADT *)

(* The operation for the >= operator is synthesized as NOT (r1 < r2) *)


END ProtoReal.