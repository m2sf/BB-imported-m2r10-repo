(* (C) 2009-2014 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

(* Specialised Blueprint for Dynamically Allocatable String ADTs *)

BLUEPRINT ProtoString [ProtoCollection];

REFERENTIAL IndexType (* ideally INTEGER or LONGINT *),
  CharType (* value type CHAR or UNICHAR *), HandlerType, LoopBodyType;

(* Relative indexing can only be used when IndexType is an integer type. *)


(* Required Module Type *)

MODULE TYPE = OPAQUE := CHAR | UNICHAR;
(* String ADTs must be opaque,
   quoted literals of base type CHAR or UNICHAR are compatible *)


(* Required Constants *)


(* Mutability *)

CONST isMutable : BOOLEAN;
(* Required constant to define whether the ADT should be mutable.
   Its value may be TRUE or FALSE. *)


(* Memory Management Model *)

CONST isRefCounted = TRUE;
(* Required constant to define whether conforming ADTs should be
   reference counted. Its value is always TRUE for Dynamic String ADTs. *)


(* Orderedness *)

CONST [DESCENDING] isOrdered = TRUE;
(* Required constant to define whether conforming ADTs should be ordered.
   Its value is always TRUE for String ADTs. *)


(* NIL Storage *)

CONST [NIL] allowsNilStorage = FALSE;
(* Required constant to define whether NIL should be a storable value.
   Its value is always FALSE for String ADTs. *)


(* Support for Invalid Accessor Retrieval Intercept *)

CONST * invAccessorRetrievalIntercept = TRUE;
(* Required constant to define whether invalid accessor retrieval errors may
   be intercepted by user defined handlers. Always TRUE for String ADTs. *)


(* Required Procedures *)


(* Introspection *)

(* Binding to Predefined Function TLIMIT *)

PROCEDURE [TLIMIT] capacityLimit : LONGCARD;
(* Required function to return the character limit of the ADT. *)

(* An expression of the form TLIMIT(StringADT))
   is synthesised as StringADT.capacityLimit(). *)


(* Binding to Predefined Function LENGTH *)

PROCEDURE [LENGTH] length ( string : ProtoString ) : LONGCARD;
(* Required function to return the length of a string. *)

(* An expression of the form LENGTH(string)
   is synthesised as StringADT.length(string). *)


(* Memory Management *)

PROCEDURE [NEW] new
  ( VAR string : ProtoString; initWith : ARRAY OF CharType );
(* Required procedure to allocate and initialise a new string. *)

(* A statement of the form NEW(string)
   is synthesised as StringADT.new(string).
   
   A statement of the form NEW(string, "foo bar baz")
   is synthesised as StringADT.new(string, "foo bar baz").  *)


isMutable ->
PROCEDURE newWithCapacity
  ( VAR string : ProtoString; capacity : IndexType );
(* Procedure to allocate a new string with a given initial capacity.
   The procedure is required when the ADT is mutable. *)


(* Binding to Predefined Procedure RETAIN *)

isRefCounted ->
PROCEDURE [RETAIN] retain ( string : ProtoString );
(* Procedure to retain a string and prevent its deallocation.
   The procedure is required when the ADT is reference counted. *)

(* A statement of the form RETAIN(string)
   is synthesised as StringADT.retain(string). *)


(* Binding to Predefined Procedure RELEASE *)

PROCEDURE [RELEASE] release ( string : ProtoString );
(* Required procedure to cancel an outstanding retain
   or deallocate a string if no retains are outstanding. *)

(* A statement of the form RELEASE(string)
   is synthesised as StringADT.release(string). *)


(* Literal Assignment *)

(* Binding to the := Operator *)

isMutable ->
PROCEDURE [:=] assignQuotedLiteral
  ( string : ProtoString; literal : ARRAY OF CharType );
(* Procedure to assign a quoted literal to a string.
   The procedure is required when the ADT is mutable. *)

(* A statement of the form string := "foo bar baz"
   is synthesised as StringADT.assignQuotedLiteral(string, "foo bar baz"). *)


(* Copying *)

(* Binding to Predefined Function DUP *)

PROCEDURE [DUP] newWithCopy ( source : ProtoString ) : ProtoString;
(* Required function to return a newly allocated copy of a string. *)

(* An expression of the form DUP(string)
   is synthesised as StringADT.newWithCopy(string). *)


(* Binding to Predefined Procedure COPY *)

isMutable ->
PROCEDURE [COPY] copy
  ( target, source : ProtoString;
    tgtStartIndex, tgtEndIndex, srcStartIndex, srcEndIndex : IndexType );
(* Procedure to destructively copy the whole or a slice of a source string,
   indicated by a given source start and end index, to the whole or a slice
   of a target string, indicated by a given target start and end index.
   The procedure is required when the ADT is mutable. *)

(* An assignment of the form
     target := source
   is synthesised as
     StringADT.copy(target, source, 0, -1, 0, -1)
   where indices 0 and -1 indicate the whole of a string.
   
   An assignment of the form
     target := source[2..5]
   is synthesised as
     StringADT.copy(target, source, 0, -1, 2, 5).
   
   An assignment of the form
     target[2..5] := source
   is synthesised as
     StringADT.copy(target, source, 2, 5, 0, -1).
   
   An assignment of the form
     target[1..4] := source[2..5]
   is synthesised as
     StringADT.copy(target, source, 1, 4, 2, 5).


(* Value Storage *)

(* Binding to Predefined Procedure STORE *)

isMutable ->
PROCEDURE [STORE] replaceCharAtIndex
 ( VAR string : ProtoString; index : IndexType; char : CharType );
(* Procedure to store a character at a given index in a string.
   The procedure is required when the ADT is mutable. *)

(* A statement of the form string[index] := char
   is equivalent to STORE(string, index, char)
   and is synthesised as StringADT.replaceCharAtIndex(string, index, char). *)


(* Value Retrieval *)

(* Binding to Predefined Function RETRIEVE *)

PROCEDURE [RETRIEVE] charAtIndex
 ( string : ProtoString; index : IndexType ) : CharType;
(* Required function to return the character
   stored at a given index in a string. *)

(* An expression of the form string[index]
   is equivalent to RETRIEVE(string, index) and
   is synthesised as StringADT.charAtIndex(string, index). *)


(* Index Out Of Range Retrieval Error Handling *)

(* A user defined index-out-of-range handler function may be used to change
   the default behaviour when RETRIEVE is called with an out-of-range index. *)

(* Handler Type *)

invAccessorRetrievalIntercept ->
TYPE HandlerType = PROCEDURE ( VAR CharType ) : BOOLEAN;
(* Required procedure type for user defined index-out-of-range handlers. *)

(* An example of an index-out-of-range handler is given below:
   
   PROCEDURE mayAbortOnIndexOutOfRange
     ( VAR charToReturn : CharType ) : BOOLEAN;
   BEGIN
     charToReturn := 0u0;
     RETURN FALSE
   END mayAbortOnIndexOutOfRange;
   
   This handler tells its caller not to abort and to return ASCII-NUL. *)


(* Handler Installation *)

invAccessorRetrievalIntercept ->
PROCEDURE installHandler ( handler : HandlerType );
(* Required procedure to install a user defined index-out-of-range handler. *)


(* Value Insertion *)

(* Binding to Predefined Procedure INSERT *)

isMutable ->
PROCEDURE [INSERT] insertCharsAtIndex
  ( targetString : ProtoString;
    index : IndexType;
    values : ARRAY OF CharType );
(* Procedure to insert characters into a target string at a given index.
   The procedure is required when the ADT is mutable. *)

(* A statement of the form INSERT(string, index, "foo" )
   is synthesised as StringADT.insertCharsAtIndex(string, index, "foo"). *)


isMutable ->
PROCEDURE insertStringAtIndex
  ( targetStr : ProtoString; index : IndexType; sourceStr : ProtoString );
(* Procedure to insert a source string into a string array at a given
   index. The procedure is required when the ADT is mutable. *)


(* Value Removal *)

(* Binding to Predefined Procedure REMOVE *)

isMutable ->
PROCEDURE [REMOVE] removeCharsAtIndex
  ( string : ProtoString; index : IndexType; charCount : IndexType );
(* Procedure to remove one or more characters from a string at a given index.
   The procedure is required when the ADT is mutable. *)

(* A statement of the form REMOVE(string, index, n)
   is synthesised as StringADT.removeCharsAtIndex(string, index, n).
   
   A statement of the form string[index] := NIL
   is treated equivalent to REMOVE(string, index, 1)
   and synthesised as StringADT.removeCharsAtIndex(string, index, 1)
   if and only if constant allowsNilStorage is FALSE. *)


(* Concatenation *)

(* Binding to Predefined Procedure CONCAT *)

isRefCounted ->
PROCEDURE [CONCAT] concat
  ( VAR result : ProtoString;
    arglist : ARGLIST OF { lit : ARRAY OF CharType; s : ProtoString } );
(* Required procedure to concatenate an alternating list of character string
   literals and string ADT variables, and return a newly allocated result
   string. Empty literals and NIL values in the argument list are ignored.
   The Procedure is required if the ADT is reference counted. *)

(* Concatenation of consecutive string literals takes place at compile time.
   Thus, string literals do not occur consecutively in run time expressions.
      
   Invocations of CONCAT are mapped to procedure StringADT.concat as follows:
   
   A statement of the form
     CONCAT(resultStr, str1, str2);
   is transformed at compile time into a procedure call of the form
     StringADT.concat(resultStr, "", str1, "", str2);

   A statement of the form
     CONCAT(resultStr, str1, "literal", str2);
   is transfored at compile time into a procedure call of the form
     StringADT.concat(resultStr, "", str1, "lit", str2);
      
   A statement of the form
     CONCAT(resultStr, "lit", str1, str2);
   is transformed at compile time into a procedure call of the form
     StringADT.concat(resultStr, "lit", str1, "", lit2);
   
   A statement of the form
     CONCAT(resultStr, "lit1", str, "lit2")
   is transformed at compile time into a procedure call of the form
     StringADT.concat(resultStr, "lit1", str, "lit2", NIL);
   
   When a concatenation expression is passed as an argument to a procedure
   it is first assigned to a temporary variable,  which is then passed as
   a parameter in the subsequent procedure call and finally released.
   
   A procedure invocation of the form
     FooProc(str1 + str2);
   is transformed at compile time into a temporary variable declaration
     VAR tempResult : StringADT;
   and a statement sequence of the form
     CONCAT(tempResult, str1, str2);
     FooProc(tempResult); RELEASE(tempResult);
   
   A function invocation of the form
     foo := BarFunc(str1 + str2);
   is transformed at compile time into a temporary variable declaration
     VAR tempResult : StringADT;
   and a statement sequence of the form   
     CONCAT(tempResult, str1, str2);
     foo := BarFunc(tempResult); RELEASE(tempResult); *)


(* Binding to the Slicing Operator *)

isRefCounted ->
PROCEDURE [..] newWithSlice
  ( source : ProtoString; startIndex, endIndex : IndexType) : ProtoString;
(* Function to return a newly allocated string and initialise it with a
   slice from a source string, indicated by a given start and end index.
   The function is required when the ADT is reference counted. *)

(* A statement of the form
     WRITE(string[2..5]);
   is synthesised as
     VAR tempString : StringADT;
     tempString := StringADT.newWithSlice(string, 2, 5);
     WRITE(tempString); RELEASE(tempString);
   
   When a slice expression appears within the R-value of an assignment,
   the assignment is synthesised using the procedure bound to COPY. *)

isRefCounted ->
PROCEDURE [..] copySlice
  ( VAR target : ProtoString; tgtStartIndex, tgtEndIndex : IndexType;
    source : ProtoString; srcStartIndex, srcEndIndex : IndexType );
(*  
   When a slice expression is passed as an argument to a procedure, it is
   first assigned to a temporary variable,  which is then passed as a
   parameter in the subsequent procedure call and finally released.

   A procedure invocation of the form
     FooProc(string[2..5]);
   is transformed at compile time into a temporary variable declaration
     VAR tempTarget : StringADT;
   and a statement sequence of the form
     StringADT.copySlice(tempTarget, 0, -1, string, 2, 5);
     FooProc(tempTarget); RELEASE(tempTarget);
   
   A function invocation of the form
     foo := BarFunc(string[2..5]);
   is transformed at compile time into a temporary variable declaration
     VAR tempTarget : StringADT;
   and a statement sequence of the form
     StringADT.copySlice(tempTarget, 0, -1, string, 2, 5);
     foo := BarFunc(tempTarget); RELEASE(tempTarget); *)


(* Iteration *)

(* Binding to FOR IN iterator *)

TYPE LoopBodyType = PROCEDURE ( VAR ProtoString, CONST IndexType );
(* Required procedure type to synthesise the body of a for loop. *)

PROCEDURE [FOR] forIterator
  ( VAR string : ProtoString; doStatements : LoopBodyType; ascending : BOOLEAN );

(* Required procedure to iterate over all characters of a string in a
   given order and execute the passed in doStatements for each character. *)
   
(* A statement of the form
     FOR index IN string DO statementSeq END
   is synthesised in two steps.
   
   The loop header is synthesised as
     StringADT.forIterator( string, forLoopBody, TRUE );
   
   The loop body is synthesised as
     PROCEDURE forLoopBody
       ( VAR string : StringADT; CONST index : IndexType ) <*INLINE*>;
     BEGIN statementSeq END forLoopBody;
   
   The loop header of a FOR DESCENDING statement is sythesised with
   FALSE passed for parameter ascending in the call to forIterator. *)


(* Relational Operations *)

(* Binding to the = and # Operators *)

PROCEDURE [=] isEqual ( string1, string2 : ProtoString ) : BOOLEAN;
(* Required function to test the equivalence of two strings. *)

(* An expression of the form string1 = string2
   is synthesised as StringADT.isEqual(string1, string2). *)

(* The operation for the # operator
   is synthesized as NOT (string1 = string2). *)


PROCEDURE [<] isLess ( string1, string2 : ProtoString ) : BOOLEAN;
(* Required procedure to test if the value of a string comes lexically
   after another. *)

(* An expression of the form string1 < string2
   is synthesised as StringADT.isLess(string1, string2). *)

(* The operation for the <= operator
   is synthesized as NOT (string1 > string2) *)


PROCEDURE [>] isGreater ( string1, string2 : ProtoString ) : BOOLEAN;
(* Required procedure to test if the value of a string comes lexically
   before another. *)

(* An expression of the form string1 > string2
   is synthesised as StringADT.isGreater(string1, string2). *)

(* The operation for the >= operator
   is synthesized as NOT (string1 < string2) *)


END ProtoString.