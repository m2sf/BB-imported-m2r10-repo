(* (C) 2009-2013 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

DEFINITION MODULE OctetIO [ProtoIO] FOR OCTET;;

(* I/O Module for Type OCTET *)

FROM FileIO IMPORT File;

(* EBNF of the textual representation of OCTET values:
    octetValue : simpleFormat |
        cBase16Format | m2Base2Format | m2Base16Format ;
    simpleFormat : base16Digit base16Digit ;
    cBase16Format : "0x" simpleFormat ;
    m2Base16Format : "0" simpleFormat "H" ;
    m2Base2Format : bit bit bit bit bit bit bit bit "B" ;
    base16Digit : "0" .. "9" | "A" .. "F" ;
    bit : "0" | "1" ; *)

PROCEDURE [READ] Read ( infile : File; VAR n : OCTET );
(* Reads the textual representation of an OCTET value from stream infile
   - any leading whitespace is skipped
   - any remaining characters that are part of the value being read are
     removed from infile
   - the textual representation of the value read is assigned to n
   - the file status is set to any of:
     success, outOfRange, wrongFormat, endOfLine, or endOfInput. This
   procedure is substituted for invocations of READ with an OCTET argument. *)

PROCEDURE [WRITE] Write ( outfile : File; CONST n : OCTET );
(* Writes the value of OCTET n in simple format to stream outfile. This
   procedure is substituted for invocations of WRITE with an OCTET argument.*)

PROCEDURE [WRITEF] WriteF
  ( outfile      : File;
    CONST fmtStr : ARRAY OF CHAR;
    items        : ARGLIST OF CONST OCTET );
(* Writes a formatted textual representation of one or more OCTET values to
   output stream outfile. The output format is determined by fmtStr. This
   procedure is substituted for invocations of WRITEF with one or more
   OCTET arguments. *)
   
PROCEDURE [READ] ReadArray ( infile : File; VAR value : ARRAY OF OCTET );
(* Calls Read repeatedly to read the textual representation of a sequence of
 OCTET values from stream infile into an array.
   - the file status is set to any of:
     success, outOfRange, wrongFormat, endOfLine, or endOfInput. This
   procedure is substituted for invocations of READ with an ARRAY OF OCTET argument.*)


PROCEDURE [WRITE] WriteArray ( outfile : File; CONST value : ARRAY OF OCTET );
(* Calls Write repeatedly to write an array  of OCTETin simple format to stream outfile.
  This procedure is substituted for invocations of WRITE with an ARRAY OF OCTET argument.*)

PROCEDURE [WRITEF] WriteArrayF
  ( outfile      : File;
    CONST fmtStr : ARRAY OF CHAR;
    items        : ARGLIST OF CONST ARRAY OF OCTET );
(* Writes a formatted textual representation of one or more ARRAY OF OCTET values to
   output stream outfile. The output format is determined by fmtStr. This
   procedure is substituted for invocations of WRITEF with one or more
   ARRAY OF OCTET arguments. *)

END OctetIO.