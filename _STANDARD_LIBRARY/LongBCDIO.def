(* (C) 2015 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

DEFINITION MODULE LongBCDIO [ProtoIO] FOR LONGBCD;

(* IO Extension Library for Type LONGBCD *)

FROM FileIO IMPORT File;


(* Bindings required for static ADTs by blueprint ProtoIO *)


PROCEDURE [READ] Read( infile : File; VAR b : LONGBCD );
(* Reads the textual representation of a LONGBCD value from input stream infile
   - any leading whitespace is skipped
   - any remaining characters that are part of the numeral being read are
     removed from infile
   - the numeric value of the numeral string read is assigned to the variable
     passed in for b
   - the file status is set to any of:
     success, outOfRange, wrongFormat, endOfLine, or endOfInput. This
     procedure is substituted for invocations of READ with a LONGBCD argument. *)

PROCEDURE [WRITE] Write( outfile : File; CONST b : LONGBCD );
(* Writes the textual representation of value b to output stream outfile. This
   procedure is substituted for invocations of WRITE with a LONGBCD argument. *)

PROCEDURE [WRITEF] WriteF
  ( outfile      : File;
    CONST fmtStr : ARRAY OF CHAR;
    items        : ARGLIST OF LONGBCD );
(* Writes a formatted textual representation of one or more LONGBCD values to
   output stream outfile. The output format is determined by fmtStr. This
   procedure is substituted for invocations of WRITEF with one or more
   LONGBCD arguments. *)


END LongBCDIO.