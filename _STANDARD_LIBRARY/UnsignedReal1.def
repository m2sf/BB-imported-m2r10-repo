(* (C) 2009-2013 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

DEFINITION MODULE UnsignedReal1 [ProtoReal];

(* Unsigned Real Number Type With Values from 0.0 to 1.0 *) 


FROM FileIO IMPORT File; (* required for IO *)


(* UnsignedReal1 type *)

TYPE
    UnsignedReal1 = OPAQUE RECORD
        value : ARRAY 4 OF OCTET; (* 32 bits *)
    END; (* UnsignedReal1 *)

    (* no sign, 30-bit significand, fixed decimal point *)


(* Bindings required by blueprint ProtoReal *)

(* Precision *)

CONST [TEXP] maxExponentDigits = 1;
(* maximum number of decimal digits of the exponent of an
   UnsignedReal1 value *)

CONST [TSIG] maxSignificandDigits = 10;
(* maximum number of decimal digits of the significand of an
   UnsignedReal1 value *)


(* Range *) 

(* TMIN(UnsignedReal1) = 0.0 *)

PROCEDURE [TMIN] minValue : UnsignedReal1;
(* returns the smallest possible value of type UnsignedReal1.
   This function is bound to pervasive function TMAX for type
   UnsignedReal1. *)

(* TMAX(UnsignedReal1) = 1.0 *)

PROCEDURE [TMAX] maxValue : UnsignedReal1;
(* returns the largest possible value of type UnsignedReal1.
   This function is bound to pervasive function TMAX for type
   UnsignedReal1. *)


(* Direct conversions to common types *)

PROCEDURE [::] toCARD  ( a : UnsignedReal1 ) : CARDINAL;
(* Converts an UnsignedReal1 value to a CARDINAL value and returns it.
   This function is bound to the :: operator for UnsignedReal1 to
   CARDINAL conversion. *)

PROCEDURE [::] toINT ( a : UnsignedReal1 ) : INTEGER; 
(* Converts an UnsignedReal1 value to an INTEGER value and returns it.
   This function is bound to the :: operator for UnsignedReal1 to
   INTEGER conversion. *)

PROCEDURE [::] toREAL ( a : UnsignedReal1 ) : REAL;
(* Converts an UnsignedReal1 value to a REAL value and returns it.
   This function is bound to the :: operator for UnsignedReal1 to
   REAL conversion. *)


(* Indirect conversions via scalar exchange format *)

PROCEDURE [SXF] toSXF ( a : UnsignedReal1; VAR s : ARRAY OF OCTET );
(* Converts an UnsignedReal1 value to a string in scalar exchange
   format.  This procedure is bound to built-in primitive SXF for
   type UnsignedReal1. *)

PROCEDURE [VAL] fromSXF ( CONST s : ARRAY OF OCTET; VAR a : UnsignedReal1 );
(* Converts a string in scalar exchange format to an UnsignedReal1
   value.  This procedure is bound to built-in primitive VAL for
   type UnsignedReal1. *)


(* Monadic arithmetic operations *) 

PROCEDURE floor ( r : UnsignedReal1 ) : UnsignedReal1;
(* Returns the floor value of r. *)

PROCEDURE ceil ( r : UnsignedReal1 ) : UnsignedReal1;
(* Returns the ceiling value of r. *)

PROCEDURE frac ( r : UnsignedReal1 ) : UnsignedReal1;
(* Returns the fractional part of r. *)


(* Dyadic arithmetic operations *) 

PROCEDURE [+] add ( r1, r2 : UnsignedReal1 ) : UnsignedReal1;
(* Adds UnsignedReal1 values r1 and r2 and returns the result.
   This function is bound to the + operator for type UnsignedReal1. *)

PROCEDURE [-] sub ( r1, r2 : UnsignedReal1 ) : UnsignedReal1;
(* Subtracts UnsignedReal1 value r2 from r1 and returns the result.
   This function is bound to the - operator for type UnsignedReal1. *)

PROCEDURE multiply ( r : UnsignedReal1; n : CARDINAL ) : UnsignedReal1;
(* Multiplies UnsignedReal1 value r by scalar n and returns the result. *)

PROCEDURE divide ( r : UnsignedReal1; n : CARDINAL ) : UnsignedReal1;
(* Divives UnsignedReal1 value r by scalar n and returns the result. *)

PROCEDURE trunc ( r : UnsignedReal1; n : CARDINAL ) : UnsignedReal1;
(* Returns the value of r truncated to n decimal places. *)

PROCEDURE round ( r : UnsignedReal1; n : CARDINAL ) : UnsignedReal1;
(* Returns the value of r rounded to n decimal places. *)


(* Relational operations *) 

PROCEDURE [=] isEqual ( r1, r2 : UnsignedReal1 ) : BOOLEAN;
(* Returns TRUE if UnsignedReal1 values r1 and r2 are equal, otherwise FALSE.
   This function is bound to operators = and # for type UnsignedReal1. *)

PROCEDURE [<] isLess ( r1, r2 : UnsignedReal1 ) : BOOLEAN;
(* Returns TRUE if UnsignedReal1 value r1 is less than r2, otherwise FALSE.
   This function is bound to operators < and >= for type UnsignedReal1. *)

PROCEDURE [>] isGreater ( r1, r2 : UnsignedReal1 ) : BOOLEAN;
(* Returns TRUE if UnsignedReal1 value r1 is greater than r2, otherwise FALSE.
   This function is bound to operators > and <= for type UnsignedReal1. *)


(* IO operations *)

PROCEDURE [READ] Read( infile : File; VAR r : UnsignedReal1 );
(* Reads the textual representation of a UnsignedReal1 value from input
   stream infile
   - any leading whitespace is skipped
   - any remaining characters that are part of the numeral being read are
     removed from infile
   - the numeric value of the numeral string read is assigned to the variable
     passed in for r
   - the file status is set to any of:
     success, outOfRange, wrongFormat, endOfLine, or endOfInput.
     This procedure is substituted for invocations of READ with an
     UnsignedReal1 argument.*)

PROCEDURE [WRITE] Write( outfile : File; r : UnsignedReal1 );
(* Writes the textual representation of value r to output stream outfile.
   This procedure is substituted for invocations of WRITE with an
   UnsignedReal1 argument.*)

PROCEDURE [WRITEF] WriteF
  ( outfile      : File;
    CONST fmtStr : ARRAY OF CHAR;
    items        : VARIADIC OF UnsignedReal1 );
(* Writes a formatted textual representation of one or more UnsignedReal1
   values to output stream outfile. The output format is determined by fmtStr.
   This procedure is substituted for invocations of WRITEF with one or more
   UnsignedReal1 arguments. *)

END UnsignedReal1.