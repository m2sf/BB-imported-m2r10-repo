(* (C) 2009-2012 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

DEFINITION MODULE INT32 [ZTYPE];

(* 32-bit Integer Numbers *) 


FROM FileIO IMPORT File; (* required for IO *)


(* INT32 type *)

TYPE
    INT32 = OPAQUE RECORD
        value : ARRAY 4 OF OCTET; (* 32 bits *)
    END; (* INT32 *)


(* Z-Type bindings to operators, pervasives and primitives: *)

(* Precision *)

CONST [MDE] maxDigitsOfExponent = 0;
(* maximum number of decimal digits of the exponent of a INT32 value *)

CONST [MDS] maxDigitsOfSignificand = 10;
(* maximum number of decimal digits of the significand of a INT32 value *)


(* Range *) 

(* TMIN(INT32) = -2,147,483,648 *)

PROCEDURE [TMIN] minValue : INT32;
(* returns the smallest possible value of type INT32.
   This function is bound to pervasive function TMAX for type INT32. *)

(* TMAX(INT32) = 2,147,483,647 *)

PROCEDURE [TMAX] maxValue : INT32;
(* returns the largest possible value of type INT32.
   This function is bound to pervasive function TMAX for type INT32. *)


(* Type conversions *)

PROCEDURE [::] toCARD  ( n : INT32 ) : CARDINAL;
(* Converts INT32 value n to a CARDINAL value and returns it. This
   function is bound to the :: operator for INT32 to CARDINAL conversion. *)

PROCEDURE [::] toINT ( n : INT32 ) : INTEGER; 
(* Converts INT32 value n to an INTEGER value and returns it. This
   function is bound to the :: operator for INT32 to INTEGER conversion. *)

PROCEDURE [::] fromCARD  ( n : CARDINAL ) : INT32;
(* Converts CARDINAL value n to an INT32 value and returns it. This function
   is bound to the :: operator for CARDINAL to INT32 conversion. *)

PROCEDURE [::] fromINT  ( n : INTEGER ) : INT32;
(* Converts INTEGER value n to an INT32 value and returns it. This function
   is bound to the :: operator for INTEGER to INT32 conversion. *)


(* Scalar conversion primitives *)

PROCEDURE [SXF] toSXF ( n : INT32; VAR s : ARRAY OF OCTET );
(* Converts INT32 value n to a string in scalar exchange format.
   This procedure is bound to pervasive primitive SXF for type INT32. *)

PROCEDURE [VAL] fromSXF ( CONST s : ARRAY OF OCTET; VAR n : INT32 );
(* Converts a string in scalar exchange format to an INT32 value.
   This procedure is bound to pervasive primitive VAL for type INT32. *)


(* Monadic arithmetic operations *) 

PROCEDURE [ABS] abs ( n : INT32 ) : INT32;
(* Returns the absolute value of INT32 value n.
   This function is bound to pervasive function ABS for type INT32. *)

PROCEDURE [NEG] neg ( n : INT32 ) : INT32;
(* Returns the sign reversed value of INT32 value n.
   This function is bound to the unary minus operator for type INT32. *)

PROCEDURE [ODD] odd ( n : INT32 ) : BOOLEAN;
(* Returns TRUE if INT32 value n is odd, otherwise FALSE.
   This function is bound to pervasive function ODD for type INT32. *)


(* Dyadic arithmetic operations *) 

PROCEDURE [+] add ( n1, n2 : INT32 ) : INT32;
(* Adds INT32 values n1 and n2 and returns the result.
   This function is bound to the + operator for type INT32. *)

PROCEDURE [-] sub ( n1, n2 : INT32 ) : INT32;
(* Subtracts INT32 value n2 from n1 and returns the result.
   This function is bound to the - operator for type INT32. *)

PROCEDURE [*] multiply ( n1, n2 : INT32 ) : INT32;
(* Multiplies INT32 values n1 and n2 and returns the result.
   This function it bound to the * operator for type INT32. *)

PROCEDURE [DIV] divide ( n1, n2 : INT32 ) : INT32;
(* Divives INT32 value n1 by n2 and returns the result.
   This function is bound to the DIV operator for type INT32. *)


(* Relational operations *) 

PROCEDURE [=] isEqual ( n1, n2 : INT32 ) : BOOLEAN;
(* Returns TRUE if INT32 values n1 and n2 are equal, otherwise FALSE.
   This function is bound to operators = and # for type INT32. *)

PROCEDURE [<] isLess ( n1, n2 : INT32 ) : BOOLEAN;
(* Returns TRUE if INT32 value n1 is less than n2, otherwise FALSE.
   This function is bound to operators < and >= for type INT32. *)

PROCEDURE [>] isGreater ( n1, n2 : INT32 ) : BOOLEAN;
(* Returns TRUE if INT32 value n1 is greater than n2, otherwise FALSE.
   This function is bound to operators > and <= for type INT32. *)


(* IO operations *)

PROCEDURE Read( infile : File; VAR n : INT32 );
(* Reads the textual representation of an INT32 value from stream infile
   - any leading whitespace is skipped
   - any remaining characters that are part of the numeral being read are
     removed from infile
   - the numeric value of the numeral string read is assigned to the variable
     passed in for n
   - the file status is set to any of:
     success, outOfRange, wrongFormat, endOfLine, or endOfInput. This
   procedure is substituted for invocations of READ with an INT32 argument. *)

PROCEDURE Write( outfile : File; n : INT32 );
(* Writes the textual representation of value n to stream outfile. This
   procedure is substituted for invocations of WRITE with an INT32 argument.*)

PROCEDURE WriteF ( outfile      : File;
                   CONST fmtStr : ARRAY OF CHAR;
                   items        : VARIADIC OF INT32 );
(* Writes a formatted textual representation of one or more INT32 values to
   output stream outfile. The output format is determined by fmtStr. This
   procedure is substituted for invocations of WRITEF with one or more
   INT32 arguments. *)

END INT32. (* thirty-two *)