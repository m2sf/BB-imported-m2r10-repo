(* (C) 2009-2016 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

DEFINITION MODULE LongCardBaseIO [ProtoIO] FOR LONGCARD;

(* Channel IO Extension Library for Values of Type LONGCARD *)

IMPORT ChanIO, StdIO;

(* EBNF of the textual representation of LONGCARD values:
    cardinalValue : simpleFormat |
        base16Format | rawBase16Format | universalFormat ;
    simpleFormat : decimalDigit+ ;
    base16Format : "0x" base16Digit+ ;
    rawBase16Format : "0" base16Digit+ ;
    universalFormat :  sign? fillChar* numeral ;
    sign : "+" ;
    fillChar : " " | "*" | "0" ;
    numeral : leadDigitGroup ( separator? digitGroup )* ;
    separator : " " | "'" | "." | "," ;
    leadDigitGroup : decimalDigit decimalDigit? decimalDigit? ;
    digitGroup : decimalDigit decimalDigit decimalDigit ;
    decimalDigit  : "0" .. "9" ;
    base16Digit : decimalDigit | "A" .. "F" ; *)

PROCEDURE [READ] Read
  ( inchan : ChanIO.Channel; VAR n : LONGCARD )
  [ inchan = StdIO.in (* default argument *) ];
(* Reads the textual representation of a LONGCARD value from channel inchan
   - any leading whitespace is skipped
   - any remaining characters that are part of the value being read are
     removed from inchan
   - the textual representation of the value read is assigned to n
   - the channel status is set to any of:
     success, outOfRange, wrongFormat, endOfLine, or endOfInput. This
   procedure is substituted for invocations of READ with a LONGCARD argument.*)

PROCEDURE [WRITE] Write
  ( outchan : ChanIO.Channel; n : LONGCARD )
  [ outchan = StdIO.out (* default argument *) ];
(* Writes the value of CARDINAL n in simple format to channel outchan. This pro-
   cedure is substituted for invocations of WRITE with a LONGCARD argument. *)

PROCEDURE [WRITEF] WriteF
  ( outchan      : ChanIO.Channel;
    CONST fmtStr : ARRAY OF CHAR;
    items        : ARGLIST >0 OF LONGCARD )
  [ outchan = StdIO.out (* default argument *) ];
(* Writes a formatted textual representation of one or more LONGCARD values to
   output channel outchan. The output format is determined by fmtStr. This
   procedure is substituted for invocations of WRITEF with one or more
   LONGCARD arguments. *)


END LongCardBaseIO.