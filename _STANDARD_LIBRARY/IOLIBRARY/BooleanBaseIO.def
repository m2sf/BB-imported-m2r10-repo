(* (C) 2009-2016 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

DEFINITION MODULE BooleanBaseIO [ProtoIO] FOR BOOLEAN;

(* IO Extension Library for Values of Type BOOLEAN *)

IMPORT ChanIO;

(* EBNF of the textual representation of BOOLEAN values:
    boolValue : trueFalseFormat | yesNoFormat ;
    trueFalseFormat : "TRUE" | "FALSE" ;
    yesNoFormat : "YES" | "NO" ; *)

PROCEDURE [READ] Read ( inchan : ChanIO.Channel; VAR b : BOOLEAN );
(* Reads the textual representation of a BOOLEAN value from channel inchan
   - any leading whitespace is skipped
   - any remaining characters that are part of the value being read are
     removed from inchan
   - the textual representation of the value read is assigned to b
   - the channel status is set to any of:
     success, outOfRange, wrongFormat, endOfLine, or endOfInput. This
   procedure is substituted for invocations of READ with a BOOLEAN argument.*)

PROCEDURE [WRITE] Write ( outchan : ChanIO.Channel; b : BOOLEAN );
(* Writes the textual representation of BOOLEAN b to channel outchan. This
   procedure is substituted for invocations of WRITE with a BOOLEAN argument.*)

PROCEDURE [WRITEF] WriteF
  ( outchan      : ChanIO.Channel;
    CONST fmtStr : ARRAY OF CHAR;
    items        : ARGLIST >0 OF BOOLEAN );
(* Writes a formatted textual representation of one or more BOOLEAN values to
   output channel outchan. The output format is determined by fmtStr. This
   procedure is substituted for invocations of WRITEF with one or more
   BOOLEAN arguments. *)

END BooleanBaseIO.