(* (C) 2009-2016 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

DEFINITION MODULE CardinalBaseFDIO [ProtoIO] FOR CARDINAL;

(* File-Descriptor IO Extension Library for Values of Type CARDINAL *)

IMPORT FileDescIO, StdFDIO;

(* EBNF of the textual representation of CARDINAL values:
    cardinalValue : simpleFormat |
        base16Format | rawBase16Format | universalFormat ;
    simpleFormat : decimalDigit+ ;
    base16Format : "0x" base16Digit+ ;
    rawBase16Format : "0" base16Digit+ ;
    universalFormat :  sign? fillChar* numeral ;
    sign : "+" ;
    fillChar : " " | "*" | "0" ;
    numeral : leadDigitGroup ( separator? digitGroup )* ;
    separator : " " | "'" | "." | "," ;
    leadDigitGroup : decimalDigit decimalDigit? decimalDigit? ;
    digitGroup : decimalDigit decimalDigit decimalDigit ;
    decimalDigit  : "0" .. "9" ;
    base16Digit : decimalDigit | "A" .. "F" ; *)

PROCEDURE [READ] Read
  ( infile : FileDescIO.File; VAR n : CARDINAL )
  [ infile = StdFDIO.in (* default argument *) ];
(* Reads the textual representation of a CARDINAL value from infile
   - any leading whitespace is skipped
   - any remaining characters that are part of the value being read are
     removed from infile
   - the textual representation of the value read is assigned to n
   - the file status is set to any of:
     success, outOfRange, wrongFormat, endOfLine, or endOfInput. This
   procedure is substituted for invocations of READ with a CARDINAL argument.*)

PROCEDURE [WRITE] Write
  ( outfile : FileDescIO.File; n : CARDINAL )
  [ outfile = StdFDIO.out (* default argument *) ];
(* Writes the value of CARDINAL n in simple format to outfile. This procedure
   is substituted for invocations of WRITE with a CARDINAL argument. *)

PROCEDURE [WRITEF] WriteF
  ( outfile      : FileDescIO.File;
    CONST fmtStr : ARRAY OF CHAR;
    items        : ARGLIST >0 OF CARDINAL )
  [ outfile = StdFDIO.out (* default argument *) ];
(* Writes a formatted textual representation of one or more CARDINAL values to
   outfile. The output format is determined by fmtStr. This procedure is sub-
   stituted for invocations of WRITEF with one or more CARDINAL arguments. *)

END CardinalBaseFDIO.