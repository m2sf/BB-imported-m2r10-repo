(* (C) 2009-2016 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

DEFINITION MODULE LongCardBaseFIO [ProtoIO] FOR LONGCARD;

(* File-Only IO Extension Library for Values of Type LONGCARD *)

IMPORT FilePtrIO;

(* EBNF of the textual representation of LONGCARD values:
    cardinalValue : simpleFormat |
        base16Format | rawBase16Format | universalFormat ;
    simpleFormat : decimalDigit+ ;
    base16Format : "0x" base16Digit+ ;
    rawBase16Format : "0" base16Digit+ ;
    universalFormat :  sign? fillChar* numeral ;
    sign : "+" ;
    fillChar : " " | "*" | "0" ;
    numeral : leadDigitGroup ( separator? digitGroup )* ;
    separator : " " | "'" | "." | "," ;
    leadDigitGroup : decimalDigit decimalDigit? decimalDigit? ;
    digitGroup : decimalDigit decimalDigit decimalDigit ;
    decimalDigit  : "0" .. "9" ;
    base16Digit : decimalDigit | "A" .. "F" ; *)

PROCEDURE [READ] Read ( infile : FilePtrIO.File; VAR n : LONGCARD );
(* Reads the textual representation of a LONGCARD value from stream infile
   - any leading whitespace is skipped
   - any remaining characters that are part of the value being read are
     removed from infile
   - the textual representation of the value read is assigned to n
   - the file status is set to any of:
     success, outOfRange, wrongFormat, endOfLine, or endOfInput. This
   procedure is substituted for invocations of READ with a LONGCARD argument.*)

PROCEDURE [WRITE] Write ( outfile : FilePtrIO.File; n : LONGCARD );
(* Writes the value of CARDINAL n in simple format to stream outfile. This pro-
   cedure is substituted for invocations of WRITE with a LONGCARD argument. *)

PROCEDURE [WRITEF] WriteF
  ( outfile      : FilePtrIO.File;
    CONST fmtStr : ARRAY OF CHAR;
    items        : ARGLIST >0 OF LONGCARD );
(* Writes a formatted textual representation of one or more LONGCARD values to
   output stream outfile. The output format is determined by fmtStr. This
   procedure is substituted for invocations of WRITEF with one or more
   LONGCARD arguments. *)


END LongCardBaseFIO.