(* (C) 2009-2013 by B.Kowarsch & R.Sutcliffe. All rights reserved. *)

DEFINITION MODULE UNISTRING [StringType];

(* Dynamic Unicode Strings *)


FROM FileIO IMPORT File; (* required for IO *)


(* UNISTRING type *)

TYPE UNISTRING = OPAQUE;


(* Bindings to special syntax, operators and pervasives. *)


(* allocator *)

PROCEDURE [NEW] new ( VAR s : UNISTRING );
(* Allocates a new object of type UNISTRING and returns a pointer to it in s.
   This procedure is bound to pervasive procedure NEW for type UNISTRING. *)

(* release reference *)

PROCEDURE [RETAIN] retain ( VAR s : UNISTRING );
(* Retains the reference to UNISTRING s.
   This procedure is bound to pervasive procedure RETAIN for type UNISTRING. *)

(* release reference *)

PROCEDURE [RELEASE] release ( VAR s : STRING );
(* Releases the reference to STRING s.  Passes NIL in s if deallocated.
   This procedure is bound to pervasive procedure RELEASE for type UNISTRING. *)


(* accessor *)

PROCEDURE [RETRIEVE] characterAtIndex ( s : UNISTRING;
                                    index : LONGCARD ) : UNICHAR;
(* Returns the character at the specified index in UNISTRING s. This
   function is bound to the rvalue array operator [ ] for type UNISTRING. *)


(* mutators *)

PROCEDURE [STORE] replaceCharAtIndex ( s : UNISTRING;
                                   index : LONGCARD;
                                      ch : UNICHAR );
(* Replaces the character at the specified index in UNISTRING s with ch. This
   procedure is bound to the lvalue array operator [ ] for type UNISTRING. *)

PROCEDURE insertCharsAtIndex ( s : UNISTRING; index : LONGCARD;
                               characters : ARRAY OF UNICHAR );
(* Inserts a character sequence into UNISTRING s at the specified index. *)

PROCEDURE removeCharsAtIndex ( s : UNISTRING; index, n : LONGCARD);
(* Removes n characters from UNISTRING s at the specified index. *)


(* length *)

PROCEDURE [LENGTH] length ( s : UNISTRING ) : LONGCARD;
(* Returns the length of string s.
   This function is bound to pervasive function LENGTH for type UNISTRING. *)


(* literal assignment *)

PROCEDURE [:=] assign ( VAR s : UNISTRING; literal : ARRAY OF CHAR );
(* Converts string literal to a UNISTRING value and assigns it to s.
   This procedure is bound to the := operator for literal assignment. *)


(* concatenation *)

PROCEDURE [+] concatenate ( s1, s2 : UNISTRING ) : UNISTRING;
(* Concatenates s1 and s2 and returns the result in a newly allocated string.
   This function is bound to the concatenate operator + for type UNISTRING. *)


(* slicing *)

PROCEDURE [..] copySlice
  ( target : UNISTRING; tgtStartIndex, tgtEndIndex : INTEGER;
    source : UNISTRING; srcStartIndex, srcEndIndex : INTEGER );
(* Copies characters  from source,  starting at srcStartIndex  and  ending at
   srcEndIndex  into target  at tgtStartIndex  but not exceeding tgtEndIndex.
   Positive indices count forward starting with zero indicating the left most
   character.  Negative indices  count backward  starting with -1  indicating
   the right most character.  If the target indices denote the entire target,
   the contents of the target  are replaced  by the source's indicated slice.
   If the target's start index is further right than its end index, the copy-
   ing order into the target slice  is reversed.  If the source's start index
   is further right  than its  end index,  the copying order  from the source
   slice is reversed.  If the source slice  is longer than  the target slice,
   only those characters that will fit into the target slice  are copied.  If
   the source slice  is shorter than  the target slice,  only  the  available
   characters of the source slice are copied. If  any index  is out of bounds
   of the associated string,  runtime fault indexOutOfBounds shall be raised.
   This procedure is bound to slice syntax str[n..m] in lvalues and rvalues. *)


(* relational operations *)

PROCEDURE [=] isEqual ( s1, s2 : UNISTRING ) : BOOLEAN;
(* Returns TRUE if the contents of s1 and s2 are equal, otherwise FALSE.
   This function is bound to operators = and # for type UNISTRING. *)

PROCEDURE [<] isLess ( s1, s2 : UNISTRING ) : BOOLEAN;
(* Returns TRUE if s1 comes lexically before s2, otherwise FALSE.
   This function is bound to operators < and >= for type UNISTRING. *)

PROCEDURE [>] isGreater ( s1, s2 : UNISTRING ) : BOOLEAN;
(* Returns TRUE if s1 comes lexically after s2, otherwise FALSE.
   This function is bound to operators > and <= for type UNISTRING. *)


(* IO procedures *)

PROCEDURE [READ] Read ( infile : File; s : UNISTRING );
(* Reads the textual representation of a UNISTRING value from stream infile
   - any leading whitespace is skipped
   - any remaining characters that are part of the string being read are
     removed from infile
   - the textual representation of the string read is assigned to the variable
     passed in for s
   - the file status is set to any of:
     success, outOfRange, wrongFormat, endOfLine, or endOfInput. This
     procedure is substituted for invocations of READ with a UNISTRING
     argument. *)

PROCEDURE [WRITE] Write ( outfile : File; s : UNISTRING );
(* Writes the textual representation of UNISTRING s to stream outfile. This
   procedure is substituted for invocations of WRITE with a UNISTRING
   argument.*)

PROCEDURE [WRITEF] WriteF
  ( outfile      : File;
    CONST fmtStr : ARRAY OF CHAR;
    items        : VARIADIC OF UNISTRING );
(* Writes a formatted textual representation of one or more UNISTRING values
   to output stream outfile. The output format is determined by fmtStr. This
   procedure is substituted for invocations of WRITEF with one or more
   UNISTRING arguments. *)

END UNISTRING.